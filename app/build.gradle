apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "org.traccar.manager"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 2
        versionName "1.0"
        multiDexEnabled = true
    }

    signingConfigs {
        release {
            storeFile file('../../android.keystore')
            keyAlias 'tananaev'
            storePassword ''
            keyPassword ''
        }
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    compile 'com.google.android.gms:play-services:8.4.0'
    compile 'com.squareup.okhttp3:okhttp-ws:3.2.0'
    compile "com.squareup.okhttp3:okhttp-urlconnection:3.2.0"
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:converter-jackson:2.0.0-beta4'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
}

task requestPasswords << {
    android.signingConfigs.release.storePassword = new String(System.console().readPassword('\nStore password: '))
    android.signingConfigs.release.keyPassword = new String(System.console().readPassword('Key password: '))
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals('packageRelease')) {
        theTask.dependsOn 'requestPasswords'
    }
}
